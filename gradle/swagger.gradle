/*
 * Plugin that provides API-first development using OpenAPI-generator to
 * generate Spring-MVC endpoint stubs at compile time from an OpenAPI definition file
 */
apply plugin: "org.openapi.generator"
buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.openapitools:openapi-generator-gradle-plugin:${openapiPluginVersion}"
    }
}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/swagger/api.yml".toString()
    outputDir = "$buildDir/openapi".toString()
    apiPackage = "com.icthh.xm.tmf.ms.customer.api"
    modelPackage = "com.icthh.xm.tmf.ms.customer.model"
    apiFilesConstrainedTo = [""]
    modelFilesConstrainedTo = [""]
    supportingFilesConstrainedTo = ["ApiUtil.java"]
    configOptions = [delegatePattern: "true", title: "customer"]
    validateSpec = true
    importMappings = [Problem:"org.zalando.problem.Problem"]
}

task openApiGenerateV31(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask){
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/swagger/api_v3.1.yml".toString()
    outputDir = "$buildDir/openapi".toString()
    apiPackage = "com.icthh.xm.tmf.ms.customer.api.v31"
    modelPackage = "com.icthh.xm.tmf.ms.customer.model.v31"
    apiFilesConstrainedTo = [""]
    modelFilesConstrainedTo = [""]
    supportingFilesConstrainedTo = ["ApiUtil.java"]
    configOptions = [delegatePattern: "true", title: "customer"]
    validateSpec = true
    importMappings = [Problem:"org.zalando.problem.Problem"]
    additionalProperties = [useTags: "true"]
}

sourceSets {
    main {
        java {
            srcDir file("${project.buildDir.path}/openapi/src/main/java")
        }
    }
}

compileJava.dependsOn("openApiGenerate")
compileJava.dependsOn("openApiGenerateV31")
